plugins {
    id 'java'
    id 'maven-publish'
    id 'application'
}

apply from: 'version.gradle'

group 'br.dev.rplus'
version loadVersion().getVersion()

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.17.2'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-csv:2.17.2'
    implementation 'org.json:json:20240303'
    implementation 'com.github.f4b6a3:uuid-creator:6.0.0'
    implementation 'com.github.f4b6a3:ulid-creator:5.2.3'

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21

    withJavadocJar()
    withSourcesJar()
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes(
                'Implementation-Title': 'Coffee Utilities Package',
                'Implementation-Version': version,
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Main-Class': 'br.dev.rplus.cup.Main'
        )
    }
}

tasks.register('fatJar', Jar) {
    group = 'build'
    description = 'Builds a JAR with dependencies (fat jar).'
    manifest.from jar.manifest
    archiveClassifier.set('aio')
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

tasks.named('sourcesJar') {
    manifest.from jar.manifest
}

tasks.named('javadocJar') {
    manifest.from jar.manifest
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifacts.clear()

            artifact(tasks.jar)
            artifact(tasks.named('sourcesJar'))
            artifact(tasks.named('javadocJar'))
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/${githubRepository}")
            credentials {
                username = 'rafandoo'
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
