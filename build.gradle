plugins {
    id 'java'
    id 'com.vanniktech.maven.publish' version '0.36.0'
    id 'dev.rafandoo.versioning' version '1.0.0'
    id 'org.jetbrains.dokka' version '2.1.0'
}

group = 'dev.rafandoo'
version = versioning.name
description = 'Coffee Utilities Package (CUP) is a Java library that provides a set of utilities for Java developers.'

ext.defaultManifestAttributes = { Project p ->
    return [
        'Manifest-Version'      : '1.0',
        'Specification-Version' : p.version,
        'Specification-Vendor'  : 'R+ Dev',
        'Implementation-Version': p.version,
        'Implementation-Vendor' : 'R+ Dev',
        'Built-Date'            : new Date().toString(),
        'Built-JDK'             : System.getProperty('java.version'),
        'Built-OS'              : System.getProperty('os.name'),
        'Built-OS-Version'      : System.getProperty('os.version'),
        'Built-OS-Architecture' : System.getProperty('os.arch')
    ]
}

ext.configurePom = { MavenPublication pub ->
    pub.pom {
        name.set(pub.artifactId)
        description.set(project.description)
        url.set('https://github.com/rafandoo/cup')

        licenses {
            license {
                name.set('Apache License, Version 2.0')
                url.set('https://raw.githubusercontent.com/rafandoo/cup/refs/heads/main/LICENSE')
                distribution.set('repo')
            }
        }

        developers {
            developer {
                id.set('rafandoo')
                name.set('Rafael Camargo')
                email.set('rafaelcamargo.inf@gmail.com')
            }
        }

        scm {
            connection.set('scm:git:git://github.com/rafandoo/cup.git')
            developerConnection.set('scm:git:ssh://git@github.com/rafandoo/cup.git')
            url.set('https://github.com/rafandoo/cup')
            tag.set('HEAD')
        }

        issueManagement {
            system.set('GitHub Issues')
            url.set('https://github.com/rafandoo/cup/issues')
        }
    }
}

dependencies {
    dokka(project(":cup-core:"))
    dokka(project(":cup-http:"))
    dokka(project(":cup-objects:"))
}

allprojects {
    apply plugin: 'org.jetbrains.dokka'

    repositories {
        mavenCentral()
        mavenLocal()
    }

    dependencies {
        dokkaPlugin 'org.jetbrains.dokka:kotlin-as-java-plugin:2.1.0'
    }

    dokka {
        pluginsConfiguration {
            html {
                footerMessage.set('Â© 2024-Present Rafael Camargo. All rights reserved.')
                customAssets.from("$rootDir/resources/dokka/cup.svg")
                customStyleSheets.from("$rootDir/resources/dokka/logo-styles.css")
            }
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.vanniktech.maven.publish'

    group = rootProject.group
    version = rootProject.version

    java {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    compileJava {
        options.encoding = 'UTF-8'
        options.compilerArgs << '-parameters'
    }

    test {
        useJUnitPlatform()
    }

    layout.buildDirectory.set(new File("$projectDir/build"))

    dependencies {
        compileOnly 'org.slf4j:slf4j-api:2.0.17'

        compileOnly 'org.projectlombok:lombok:1.18.42'
        annotationProcessor 'org.projectlombok:lombok:1.18.42'

        testCompileOnly 'org.projectlombok:lombok:1.18.42'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.42'
    }

    tasks.withType(Jar).configureEach {
        manifest {
            attributes defaultManifestAttributes(project) + [
                'Specification-Title' : "Coffee Utilities Package - ${project.name.replace('cup-', '').toUpperCase()}",
                'Implementation-Title': "Coffee Utilities Package - ${project.name.replace('cup-', '').toUpperCase()}"
            ]
        }
        exclude '**/.gitkeep'
    }

    plugins.withId('com.vanniktech.maven.publish') {
        publishing {
            publications.withType(MavenPublication).configureEach { pub -> configurePom(pub) }
        }
    }

    mavenPublishing {
        publishToMavenCentral()
        signAllPublications()
    }
}

tasks.register('fatJar', Jar) {
    group = 'build'
    description = 'Builds a JAR with dependencies (Fat Jar).'

    archiveBaseName.set('cup')
    archiveVersion.set(project.version.toString())
    archiveClassifier.set('aio')

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    from subprojects.collect { it.sourceSets.main.output }

    from {
        subprojects.collectMany {
            it.configurations.runtimeClasspath
                .filter { it.name.endsWith('.jar') }
                .collect { zipTree(it) }
        }
    }

    manifest {
        attributes defaultManifestAttributes(project) + [
            'Specification-Title' : 'Coffee Utilities Package',
            'Implementation-Title': 'Coffee Utilities Package'
        ]
    }

    destinationDirectory.set(layout.projectDirectory.dir('artifacts'))
}

tasks.register('exportToRawMaven', Copy) {
    group = 'publishing'
    description = 'Exports artifacts from Maven Local.'

    dependsOn subprojects.collect { it.tasks.named('publishToMavenLocal') }

    def localMaven = new File(System.getProperty('user.home'), '.m2/repository/dev/rafandoo')

    from localMaven
    into layout.buildDirectory.dir('raw-mvn-repo/dev/rafandoo')
}

